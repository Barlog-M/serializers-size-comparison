allprojects {
	group 'com.example'
	version '0.1-SNAPSHOT'
}

subprojects {
	apply plugin: 'java'

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	repositories {
		jcenter()
	}
}

project(':comparator') {
	apply plugin: 'application'

	mainClassName = 'com.example.App'

	dependencies {
		compile project(':thrift'), project(':xml'), project(':msgpack')
		compile 'com.ibm.icu:icu4j:+'
		compile 'org.slf4j:slf4j-jcl:+'
		compile 'org.tukaani:xz:+'
	}

	jar {
		from {
			configurations.compile.collect {it.isDirectory() ? it : zipTree(it)}
			configurations.runtime.collect {it.isDirectory() ? it : zipTree(it)}
		}

		manifest {
			attributes(
					'Main-Class': mainClassName,
					'Implementation-Title': rootProject.name,
					'Implementation-Version': version,
					'Implementation-Vendor': 'barlog'
			)
		}
	}
}

project(':thrift') {
	dependencies {
		compile('org.apache.thrift:libthrift:+') {
			exclude group: 'org.apache.httpcomponents'
			exclude group: 'org.slf4j', module: 'slf4j-api'
		}
		compile 'org.slf4j:slf4j-api:+'
	}
}

project(':xml') {

}

project(':msgpack') {
	dependencies {
		compile 'org.msgpack:msgpack:+'
	}
}

wrapper {
	gradleVersion = '2.7'
}

